module main // must be named same as the file!

import src/lib
import io/console

// def main(): Unit = println(helloWorld())

effect Reveal(row: Int, col: Int): Board
effect GameOver(): Board
effect Flag(row: Int, col: Int): Board

type Cell {
  Hidden(hasMine: Bool, isFlagged: Bool)
  Revealed(isMine: Bool, adjacentMines: Int)
}

record Board(grid: Array[Array[Cell]], rows: Int, cols: Int, mines: Int)

def main() = {
  var board = initializeBoard(10, 10, 10)

  try {
    playGame(board)
  } with Reveal { (row, col) =>
    println("Revealing cell at (" ++ show(row) ++ ", " ++ show(col) ++ ")")
    try {resume(revealCell(board, row, col))}
    with GameOver {println("Game Over! You hit a mine.")}
  }
  with Flag { (row, col) =>
    println("Flagging cell at (" ++ show(row) ++ ", " ++ show(col) ++ ")")
    resume(flagCell(board, row, col))
  }
  println("over")
}

def initializeBoard(rows: Int, cols: Int, mines: Int): Board = {
  with on[OutOfBounds].panic
  var grid: Array[Array[Cell]] = allocate(rows)
  each(0,rows) {curr =>
    var row: Array[Cell] = allocate(cols)
    each(0,cols) {cell =>
      row.set(cell, Hidden(if (cell == 3) true else false, false))
    }
    grid.set(curr, row)
  }
  Board(grid, rows, cols, mines)
}

def playGame(board: Board): Unit / {Flag, Reveal} = {
  with on[WrongFormat].panic
  var board = board
  while (true) {
    printBoard(board)
    val input = askUser("Enter action (r x y to reveal, f x y to flag): ")
    board = input.split(" ") match {
      case Cons("r", Cons(x, Cons(y, _)))  => do Reveal(y.toInt, x.toInt)
      case Cons("f", Cons(x, Cons(y, _))) => do Flag(y.toInt, x.toInt)
      case _ =>
        println("Invalid input. Try again.")
        board
    }
  }
}

def flagCell(board: Board, row: Int, col: Int): Board / {} = {
  with on[OutOfBounds].panic
  val cell = board.grid.get(row).get(col)
  cell match {
    case Hidden(hasMine, isFlagged) =>
      board.grid.get(row).set(col, Hidden(hasMine, not(isFlagged)))
    case Revealed(_, _) =>
      println("Cannot flag a revealed cell!")
  }
  board
}

def revealCell(board: Board, row: Int, col: Int): Board / {GameOver} = {
  with on[OutOfBounds].panic
  val cell = board.grid.get(row).get(col)
  cell match {
    case Hidden(hasMine, _) =>
      if (hasMine) do GameOver()
      else {
        val adjMines = countAdjacentMines(board, row, col)
        board.grid.get(row).set(col, Revealed(false, adjMines))
        if (adjMines == 0) revealAdjacentCells(board, row, col) else board
      }
    case _ => board
  }
}

def countAdjacentMines(board: Board, row: Int, col: Int): Int = {
  with on[OutOfBounds].panic
  var counter = 0
  neighbors(row, col, board.rows, board.cols).foreach {
    case (r, c) => board.grid.get(r).get(c) match {
      case Hidden(true, _) => counter = counter + 1
      case _               => ()
    }
  }
  counter
}

effect offset(): Int

def neighbors(row: Int, col: Int, rows: Int, cols: Int): List[(Int, Int)] = {
  var list: List[(Int,Int)] = empty()
  try {
    val dr = do offset()
    val dc = do offset()
    val nr = row + dr
    val nc = col + dc
    if ((nr >= 0 && nr < rows && nc >= 0 && nc < cols) and not(dr == 0 && dc == 0)) list = Cons((nr,nc),list)
  } with offset {
    def offset() = each(-1,2){v => resume(v)}
  }
  list.reverse
}

def revealAdjacentCells(board: Board, row: Int, col: Int): Board / {GameOver} = {
  neighbors(row, col, board.rows, board.cols).foldLeft(board) {
    (b, coord) => revealCell(b, coord.first, coord.second)
  }
}

def printBoard(board: Board): Unit = {
  var str = ""
  var counter = 0
  str = str ++ "\n"
  str = str ++ "*|"
  each(0, board.cols) {n => str = str ++ show(n) ++ "|"}
  foreach(board.grid) {row =>
    str = str ++ "\n"
    str = str ++ show(counter) ++ "|"
    counter = counter + 1
    foreach(row) {elem =>
      str = str ++ (elem match {
        case Hidden(_, true)       => "F"
        case Hidden(_, false)      => "â—¼"
        case Revealed(_, count) and (count > 0) => show(count)
        case Revealed(_, _) => " "
      }) ++ "|"
    }
  }
  println(str)
}

def askUser(prompt: String): String = {
  with console
  println(prompt)
  do readLine()
}