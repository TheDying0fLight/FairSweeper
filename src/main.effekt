module main // must be named same as the file!

import io/console
import tty

import src/board
import src/utils

def main() = {
  var board = initializeBoard(4, 4, 3)
  println(Screen::clear())

  try {playGame(board)}
  with GameOver {(_,_) => println("Game Over! You hit a mine."); println(board, true)}
  with Won {_ => println("You WON!"); println(board, true)}
  with Revealing { xy =>
    println("Revealing cell at (" ++ show(xy.x) ++ ", " ++ show(xy.y) ++ ")")
    resume(())
  }
  with Flagging { xy =>
    println("Flagging cell at (" ++ show(xy.x) ++ ", " ++ show(xy.y) ++ ")")
    resume(())
  }
  println("over")
}

def playGame(board: Board): Unit / {Flagging, Revealing, Won, GameOver} = {
  while (true) {
    println(board)
    val input = askUser("Enter action (r x y to reveal, f x y to flag): ")
    println(Screen::clear())
    with on[WrongFormat].default{println("Coordinates need to be integer. Try again")}
    input.split(" ") match {
      // todo xy must be largereq 0 and lesseq board.rows/cols
      case Cons("r", Cons(x, Cons(y, _))) => board.revealCell(XY(x.toInt, y.toInt), true, true)
      case Cons("f", Cons(x, Cons(y, _))) => board.flagCell(XY(x.toInt, y.toInt))
      case _ => println("Invalid input. Try again.")
    }
  }
}

def askUser(prompt: String): String = {
  with console
  println(prompt)
  do readLine()
}