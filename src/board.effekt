import src/utils

effect Revealing(cell: XY): Unit
effect Flagging(cell: XY): Unit
effect GameOver(board: Board, move: XY): Unit
effect Won(board: Board): Unit

// for resuming with each value with each()
effect Offset(start: Int, end: Int): Int

record Board(grid: Array[Array[Cell]], rows: Int, cols: Int, mines: Int)

def getCell(board: Board, xy: XY): Cell = {
  with on[OutOfBounds].panic
  board.grid.get(xy.y).get(xy.x)
}

def setCell(board: Board, xy: XY, new_val: Cell): Unit = {
  with on[OutOfBounds].panic
  board.grid.get(xy.y).set(xy.x, new_val)
}

def buildBoard(rows: Int, cols: Int, mines: List[XY]): Board = {
  with on[OutOfBounds].panic
  var grid: Array[Array[Cell]] = allocate(rows)
  each(0,rows) {rw =>
    grid.set(rw, array(cols, Hidden(false, false)))
  }
  val board = Board(grid, rows, cols, mines.size())
  mines.foreach{cell => board.setCell(cell, Hidden(true, false))}
  board
}

def initializeBoard(rows: Int, cols: Int, mines: Int): Board = {
  var cells: List[XY] = empty()
  try {
    val y = do Offset(0, rows)
    val x = do Offset(0, cols)
    cells = cells.append([XY(x,y)])
  } with Offset {(s,e) =>
    each(s,e) {v => resume(v)}
  }
  while (cells.size > mines) {
    // +1 so we can also hit the last cell
    val idx = (random() * (cells.size + 1).toDouble).floor
    cells = cells.deleteAt(idx)
  }
  buildBoard(rows, cols, cells)
}

def getXY(board: Board) {f: Cell => Bool}: List[XY] = {
  board.collect{ (xy, cell) =>
    if (f(cell)) Some(xy)
    else None()
  }
}

def foreach(board: Board) {f: (XY, Cell) => Unit}: Unit = {
  try {
    val x = do Offset(0, board.cols)
    val y = do Offset(0, board.rows)
    val xy = XY(x,y)
    val cell = board.getCell(xy)
    f(xy, cell)
  } with Offset {(s,e) =>
    each(s,e) {v => resume(v)}
  }
}

def collect[A](board: Board) {f: (XY, Cell) => Option[A]}: List[A] = {
  var acc: List[A] = empty()
  board.foreach{ (a,b) =>
    f(a,b) match {
      case Some(v) => acc = Cons(v,acc)
      case None()  => ()
    }
  }
  acc.reverse
}

def copy(board: Board): Board = {
  with on[OutOfBounds].panic
  var newGrid = board.grid.copy
  newGrid.foreachIndex{ (index,row) =>
    newGrid.set(index,row.copy)
  }
  Board(newGrid, board.rows, board.cols, board.mines)
}

def hideHiddenInfo(board: Board): Unit =
  board.foreach{ (xy,cell) =>
    board.getCell(xy) match {
      case Hidden(_,_) => board.setCell(xy, TrueHidden())
      case _ => ()
    }
  }

def revealCell(board: Board, xy: XY, eff: Bool): Unit / {GameOver, Won, Revealing} = {
  val cell = board.getCell(xy)
  cell match {
    case Hidden(hasMine, _) =>
      if (eff) do Revealing(xy)
      if (hasMine) do GameOver(board, xy)
      else {
        val adjMines = countAdjacentMines(board, xy)
        board.setCell(xy, Revealed(adjMines))
        if (adjMines == 0) revealAdjacentCells(board, xy)
      }
      val hidden = board.getXY{
        case Hidden(_,_) => true
        case _ => false
      }
      if (hidden.size == board.mines) do Won(board)
    case _ => ()
  }
}
def revealCell(board: Board, xy: XY): Unit / {GameOver, Won, Revealing} = board.revealCell(xy,false)
def revealCellEffectless(board: Board, xy: XY): Unit / {} = {
  try {board.revealCell(xy)}
  with GameOver {(_,_) => ()}
  with Won {_ => ()}
  with Revealing {_ => resume(())}
}

def countAdjacentMines(board: Board, xy: XY): Int =
  board.getNeighbors(xy, 1).collect{cel =>
    if (board.getCell(cel) is Hidden(true, _)) Some(xy)
    else None()
  }.size

def revealAdjacentCells(board: Board, xy: XY): Unit / {GameOver, Won, Revealing} = {
  board.getNeighbors(xy, 1).foreach{ xy => board.revealCell(xy) }
}

def flagCell(board: Board, xy: XY): Unit / {Flagging} = {
  val cell = board.getCell(xy)
  cell match {
    case Hidden(hasMine, isFlagged) =>
      do Flagging(xy)
      board.setCell(xy, Hidden(hasMine, not(isFlagged)))
    case Revealed(_) =>
      println("Cannot flag a revealed cell!")
    case _ => <>
  }
}
def flagCellEffectless(board: Board, xy: XY): Unit / {} = {
  try {board.flagCell(xy)}
  with Flagging {_ => resume(())}
}

def println(board: Board, showBombs: Bool): Unit = {
  var str = ""
  var counter = 0
  // str = str ++ "\n"
  str = str ++ "*|"
  each(0, board.cols) {n => str = str ++ n.mod(10).show ++ "|"}
  foreach(board.grid) {row =>
    str = str ++ "\n"
    str = str ++ counter.mod(10).show ++ "|"
    counter = counter + 1
    foreach(row) {elem =>
      str = str ++ (elem match {
        case Hidden(true, _) and showBombs      => "B"
        case Hidden(_, true)                    => "F"
        case Hidden(_, false)                   => "â—¼"
        case Revealed(count) and (count > 0)    => count.show
        case Revealed(_)                        => " "
        case NoMine()                           => "N"
        case Mine()                             => "M"
        case TrueHidden()                       => "T"
      }) ++ "|"
    }
  }
  println(str)
}
def println(board: Board): Unit = println(board, false)

def isValid(board: Board, xy: XY): Bool = {
  board.getRevealedNeighbors(xy).all{ xy2 =>
    val mineCheck = board.mineCount(xy2)
    board.getCell(xy2) match {
      case Revealed(c) =>
        if (mineCheck.second) c == mineCheck.first else c >= mineCheck.first
      case _ => println("How did we get here"); <>
    }
  }
}

def mineCount(board: Board, xy: XY): (Int,Bool) = {
  var noTrueHiddenNbs = true
  (board.getNeighbors(xy, 1).collect{ xy =>
    board.getCell(xy) match {
      case Mine() => Some(true)
      case TrueHidden() => noTrueHiddenNbs = false; None()
      case _ => None()
    }
  }.size, noTrueHiddenNbs)
}

// ------------------------- GETTER ------------------------
def getRevealedNeighbors(board: Board, xy: XY): List[XY] = {
  board.getNeighbors(xy, 1).collect{ xy =>
    board.getCell(xy) match {
      case Revealed(_) => Some(xy)
      case _ => None()
    }
  }
}

def getHiddenNeighbors(board: Board, xy: XY): List[XY] = {
  board.getNeighbors(xy, 1).collect{ xy =>
    board.getCell(xy) match {
      case TrueHidden() => Some(xy)
      case _ => None()
    }
  }
}

def getMineNeighbors(board: Board, xy: XY): List[XY] = {
  board.getNeighbors(xy, 1).collect{ xy =>
    board.getCell(xy) match {
      case Mine() => Some(xy)
      case _ => None()
    }
  }
}

def getMines(board: Board): List[XY] =
  board.collect{ (xy,cell) =>
    cell match {
      case Mine() => Some(xy)
      case _ => None()
    }
  }

def getRevealed(board: Board): List[XY] =
  board.getXY{
    case Revealed(_) => true
    case _ => false
  }

def getNeighbors(board: Board, xy: XY, size: Int): List[XY] = {
  var list: List[XY] = empty()
  try {
    val dr = do Offset(0 - size, size + 1)
    val dc = do Offset(0 - size, size + 1)
    val nr = xy.y + dr
    val nc = xy.x + dc
    if ((nr >= 0 && nr < board.rows && nc >= 0 && nc < board.cols) and not(equals((dc,dr),(0,0))))
      list = Cons(XY(nc,nr),list)
  } with Offset {(s,e) =>
    each(s,e){v => resume(v)}
  }
  list.reverse
}